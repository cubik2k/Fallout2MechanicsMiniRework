#define SFALL_SC_EVALUATION   (true)  // short - circuit evaluation

#include "DEFINE.H"
#include "sfall.h"

variable AntiCrit

procedure COMBATDAMAGE begin
   variable target = get_sfall_arg;
   variable attacker = get_sfall_arg;
   variable dmgtotar = get_sfall_arg_at(2);
   variable dmgtoatkr = get_sfall_arg_at(3);
   variable tar_flags = get_sfall_arg_at(4);
   variable atkr_flags = get_sfall_arg_at(5);
   if dmgtotar < AntiCrit andAlso (atkr_flags bwand DAM_HIT) andAlso (atkr_flags bwand DAM_CRITICAL) andAlso not(atkr_flags bwand DAM_BACKWASH) then begin
      set_sfall_return(dmgtotar);
      set_sfall_arg(2, dmgtotar);
      set_sfall_return(dmgtoatkr);
      set_sfall_arg(3, dmgtoatkr);
      set_sfall_return(0);
      set_sfall_arg(4, 0);
      set_sfall_return(DAM_HIT);
      set_sfall_arg(5, DAM_HIT);
   end
end

procedure start begin
   if game_loaded then begin
      AntiCrit = get_ini_setting("mods\\F2MechanicsMiniRework.ini|MAIN|AntiCrit");
      if AntiCrit > 0 then begin
         register_hook_proc_spec(HOOK_COMBATDAMAGE, COMBATDAMAGE);
         set_global_script_type(0);
         set_global_script_repeat(0);
      end
   end
end